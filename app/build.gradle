import com.example.mvvm_template.build.Dep

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.mvvm_template"
        minSdk 22
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        //noinspection DataBindingWithoutKapt
        enabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

configurations {
    ktlint
}

// Add コードスタイルをチェック
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    // main = "com.pinterest.ktlint.Main" は非推奨
    mainClass.set("com.pinterest.ktlint.Main")
    args "src/**/*.kt", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // 途中終了させない
    ignoreExitValue true
}

// Add
check.dependsOn ktlint

// Add 自動でフォーマットしてくれる
task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    // main = "com.pinterest.ktlint.Main" は非推奨
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
    // 途中終了させない
    ignoreExitValue true
}

dependencies {

    // Androidx
    implementation Dep.Androidx.core
    implementation Dep.Androidx.appcompat
    implementation Dep.Androidx.constraintlayout
    implementation Dep.Google.material
    implementation Dep.Androidx.fragment

    // デバッグ用
    implementation Dep.Log.timber

    // DI
    implementation Dep.Dagger.core
    implementation Dep.Dagger.android
    implementation Dep.Dagger.androidSupport
    annotationProcessor Dep.Dagger.compiler
    annotationProcessor Dep.Dagger.androidProcessor
    kapt Dep.Dagger.compiler
    kapt Dep.Dagger.androidProcessor

    // lifecycle
    implementation Dep.Lifecycle.livedata_ktx
    implementation Dep.Lifecycle.runtime_ktx
    implementation Dep.Lifecycle.common_java8
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt Dep.Lifecycle.compiler

    // test module
    testImplementation Dep.Junit.junit
    testImplementation Dep.Androidx.Test.core
    androidTestImplementation Dep.Androidx.Test.extJunit
    androidTestImplementation Dep.Androidx.Test.espressoCore
    androidTestImplementation Dep.Androidx.Test.runner
    androidTestImplementation Dep.Androidx.Test.rules
    androidTestImplementation Dep.Androidx.Test.espressoIntents
    androidTestImplementation Dep.Mockito.core

    // ktlint
    ktlint "com.pinterest:ktlint:0.47.1"
}